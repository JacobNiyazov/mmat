{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "884ca266-c859-4b77-b520-0b40063723f1",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# MSCF 46982 Market Microstructure and Algorithmic Trading\n",
    "\n",
    "Fall 2025 Mini 2\n",
    "\n",
    "Price Impact of Order Book Events\n",
    "\n",
    "Copyright &copy; 2025 Nick Psaris. All Rights Reserved"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "165d290b-fa6b-4a06-bc26-1afe69c634a7",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# TOC\n",
    "- [Initialize](#Initialize)\n",
    "- [The Price Impact of Order Book Events](#The-Price-Impact-of-Order-Book-Events)\n",
    "- [Order Book Events](#Order-Book-Events)\n",
    "- [Order Flow Imbalance](#Order-Flow-Imbalance)\n",
    "- [Market Depth](#Market-Depth)\n",
    "- [Calculating the Impact of Order Flow Imbalance](#Calculating-the-Impact-of-Order-Flow-Imbalance)\n",
    "- [Modeling the Price Impact of Order Flow Based on Market Depth](#Modeling-the-Price-Impact-of-Order-Flow-Based-on-Market-Depth)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2ecad19a-c813-4945-993f-bdbcdcc9a7ee",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Initialize\n",
    "- We start by initializing the number of rows and columns displayed\n",
    "- Then create connection to the taq Kdb+ server\n",
    "- And finally define a few utility functions (rnd, pivot and ols)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "47a98f79-f646-469a-830c-10bdadeb44aa",
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "PyKX now running in 'jupyter_qfirst' mode. All cells by default will be run as q code. \n",
      "Include '%%py' at the beginning of each cell to run as python code. \n"
     ]
    }
   ],
   "source": [
    "import os\n",
    "os.environ['PYKX_JUPYTERQ'] = 'true'\n",
    "os.environ['PYKX_4_1_ENABLED'] = 'true'\n",
    "import pykx as kx\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "4074db62-fc00-426c-bf44-09627c84b5da",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "`:/Users/nick/cmu_userpass.txt\n"
     ]
    }
   ],
   "source": [
    "\\c 15 120\n",
    "/ windows and mac/linux use different environment variables\n",
    "home:`HOME`USERPROFILE \"w\"=first string .z.o\n",
    "upf:0N!` sv (hsym`$getenv home),`cmu_userpass.txt\n",
    "h:`$\":tcps://tpr-mscf-kx.tepper.cmu.edu:5000:\",first read0 upf\n",
    "rnd:{x*\"j\"$y%x}\n",
    "pivot:{[t]\n",
    " u:`$string asc distinct last f:flip key t;\n",
    " pf:{x#(`$string y)!z};\n",
    " p:?[t;();g!g:-1_ k;(pf;`u;last k:key f;last key flip value t)];\n",
    " p}\n",
    "ols:{[i;Y;X]\n",
    " if[type Y;:.z.s[i;enlist Y;X]];\n",
    " if[type X;:.z.s[i;Y;enlist X]];\n",
    " if[i;X:enlist[count[X 0]#1f],X];\n",
    " THETA:Y lsq X;\n",
    " THETA}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2ed4f892-a1fc-4afa-afe1-2bb8ffb7ec57",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# The Price Impact of Order Book Events\n",
    "\n",
    "Cont, Rama and Kukanov, Arseniy and Stoikov, Sasha, [The Price Impact\n",
    "of Order Book Events][] (April 30, 2012). JOURNAL OF FINANCIAL\n",
    "ECONOMETRICS (Winter 2014) 12 (1): 47-88.\n",
    "\n",
    "[The Price Impact of Order Book Events]: http://dx.doi.org/10.2139/ssrn.1712822 \"The Price Impact of Order Book Events\"\n",
    "\n",
    "- Rama Cont, Arseniy Kukanov, Sasha Stoikov demonstrate that over\n",
    "  short time intervals price changes are driven by order flow\n",
    "  imbalances (OFI)\n",
    "- The price impact of order flow encompasses the price impact of trades\n",
    "  because trades impact the order book and decrease supply\n",
    "- Cont et al. provide a model that depends on **average depth** which\n",
    "  is defined as average sum of bid/ask sizes\n",
    "- In principle, there should be more information in order book events\n",
    "  because there are many more of them\n",
    "- In practice, the importance of order book events depends on how\n",
    "  **reliable** they are\n",
    "- [Some have speculated][] that order flow imbalances have no\n",
    "  explanatory power in the cryptcurrency markets because there are\n",
    "  **no regulations that prevent spoofing**\n",
    "\n",
    "[Some have speculated]: https://medium.com/@eliquinox/order-flow-analysis-of-cryptocurrency-markets-b479a0216ad8\n",
    "\n",
    " "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "54e9796f-573a-4ad3-803d-a01cd83e19dc",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Order Book Events\n",
    "\n",
    "- The order flow contribution for a single event can be described by\n",
    "changes in bid/ask prices and sizes\n",
    "$$\n",
    "e_n = I_{P_n^B\\ge P_{n-1}^B}q_n^B \n",
    "    - I_{P_n^B\\le P_{n-1}^B}q_{n-1}^B\n",
    "    - I_{P_n^A\\le P_{n-1}^A}q_n^BA \n",
    "    + I_{P_n^A\\ge P_{n-1}^A}q_{n-1}^A\n",
    "$$\n",
    "where $I$ is an indicator to determine the state, $P^B$ and $P^A$ are\n",
    "the bid/ask prices, $q^B$ and $q^A$ are the bid/ask sizes and $n$ is\n",
    "the current state while $n-1$ is the previous state\n",
    "\n",
    "- **Increases** in bid prices or sizes indicate an **increases** in\n",
    "  demand\n",
    "- **Decreases** in bid prices or sizes indicate a **decreases** in\n",
    "  demand\n",
    "- **Increases** in ask prices or **decreases** in ask sizes indicate a\n",
    "  **decrease** in supply\n",
    "- **Decreases** in ask prices or **increases** in ask sizes indicates an\n",
    "  **increases** in supply\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "f655d2c3-76df-4f68-9bb9-c46522a9a69c",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [],
   "source": [
    "ofi:{[b;bs;a;as]\n",
    " e:               bs *b>=pb:prev[first b;b];\n",
    " e-:prev[first bs;bs]*b<=pb;\n",
    " e-:              as *a<=pa:prev[first a;a];\n",
    " e+:prev[first as;as]*a>=pa;\n",
    " e}"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "777dc732-1430-4938-b48d-b4fbd7dd9261",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Order Flow Imbalance\n",
    "\n",
    "- The order flow imbalance is the cumulative effect of all events\n",
    "  between time $t_k-1$ and $t_k$\n",
    "$$\n",
    "\\text{OFI}_k = \\sum_{n=N(t_{k-1})+1}^{N(t_k)} e_n\n",
    "$$\n",
    "where $N(t_k)$ is the Nth order book event corresponding to the time $t_k$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c1bac26e-ed2f-4f47-92dc-4fb869c252c6",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "source": [
    "- To describe the impact on prices, Cont et al. define the price change (in ticks) between time $t_k-1$ and $t_k$ as\n",
    "$$\n",
    "\\Delta P_k = (P_k - P_{k-1})/\\delta\n",
    "$$\n",
    "where $\\delta$ is the tick size\n",
    "- The model specifies a simple linear relationship between $\\Delta\n",
    "  P_k$ and $\\text{OFI}_k$\n",
    "$$\n",
    "\\Delta P_k = \\alpha + \\beta \\hspace{0.5em} \\text{OFI}_k + \\epsilon_k\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "86fc66f7-78b7-4180-84c5-c4a007c07d65",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Market Depth\n",
    "\n",
    "- Cont et al. also define 'market depth' as the average size of the\n",
    "  best bid and offer quote sizes (which we will use later)\n",
    "$$\n",
    "\\text{AD}_i = \\frac{1}{2N(T_i) - N(T_{i-1})-1} \\sum_{n=N(T_{i-1})+1}^{N(T_i)} (q_n^B + q_n^A)\n",
    "$$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6dca756f-8fa3-432c-8245-dbf917a1967d",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Calculating the Impact of Order Flow Imbalance\n",
    "\n",
    "- To remove abnormal trading behavior, Cont et al. exclude the first\n",
    "  and last 30 minutes of trading\n",
    "- The implementation computes the order flow imbalance (OFI) every 10\n",
    "  seconds\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4af31267-1285-48f5-83c4-f55d4332c189",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "subslide"
    },
    "tags": []
   },
   "source": [
    "## Impact of order flow imbalance results\n",
    "\n",
    "- Regressions are performed every 30 minutes and then averaged\n",
    "- We can see how low (high) priced securities move a small (large)\n",
    "  amount for changes in the order flow\n",
    "- We use notional (not price) to perform a cross-security comparisons\n",
    "- Observe, also, that this is a **contemporaneous model**, and does\n",
    "  not attempt to make predictions on future price changes based on\n",
    "  current order flow imbalances\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "81c5ec9c-b737-4b47-83ac-e747de5481dd",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [],
   "source": [
    "orderflow:{[ofif;dt;syms]\n",
    " t:select from nbbo where date = dt, sym in syms, time within 0D10 0D15:30, asize>0,bsize>0;\n",
    " t:update mid:.5*bid+ask,ofi:ofif[bid;bsize;ask;asize] by sym from t;\n",
    " s:select o:first mid, c:last mid, sum ofi, ad:avg .5*bsize+asize by sym,0D00:00:10 xbar time from t;\n",
    " s:update dticks:100*deltas[first o;c] by sym from s;\n",
    " s}\n",
    "s:h (orderflow;ofi;2020.02.03;`AMZN`TSLA`NFLX`ORCL`CSCO`MU`BAC)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "b7653138-07c3-4ebd-b942-f878d0186a69",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "BAC | 0.0005664369\n",
      "CSCO| 0.004715658\n",
      "ORCL| 0.00481793\n",
      "MU  | 0.008105073\n",
      "NFLX| 0.4398638\n",
      "TSLA| 1.169921\n",
      "AMZN| 2.773596\n"
     ]
    }
   ],
   "source": [
    "asc avg pivot b:select avg ad, beta:last last ols[1b;dticks;\"f\"$ofi] by 0D00:30 xbar time, sym from s"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6c84a8e5-d4b0-447b-9feb-456cb770ed54",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "slide"
    },
    "tags": []
   },
   "source": [
    "# Modeling the Price Impact of Order Flow Based on Market Depth\n",
    "\n",
    "- Using the average depth (AD), a relationship is found between the\n",
    "  price impact coefficeint $\\beta$ and the market depth $AD$\n",
    "$$\n",
    "\\beta_i = \\frac{c}{\\text{AD}_i^\\lambda }+ \\nu_i\n",
    "$$\n",
    "where $c$ and $\\lambda$ are constants and $\\nu_i$ is a noise term"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "613db448-609d-4e79-a4ad-883478cdbf60",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "subslide"
    },
    "tags": []
   },
   "source": [
    "## Fit $\\lambda$ coefficient\n",
    "\n",
    "- $\\lambda$ is fit first with a nonlinear regression by using linear\n",
    "  regression on the **log values**\n",
    "$$\n",
    "\\log \\hat{\\beta_i} = \\hat{\\alpha}_{L,i} - \\hat{\\lambda}\\log\\text{AD}_i + \\hat{\\epsilon}_{L,i}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "d7ce2f6a-c487-4d7b-a1c5-553706d9cc12",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sym | lambda   \n",
      "----| ---------\n",
      "AMZN| 1.051458 \n",
      "BAC | 1.399238 \n",
      "CSCO| 1.471654 \n",
      "MU  | 1.045026 \n",
      "NFLX| 0.2949058\n",
      "ORCL| 0.9505975\n",
      "TSLA| 0.2956302\n"
     ]
    }
   ],
   "source": [
    "show l:select lambda:last last ols[1b;log beta;neg log ad] by sym from b"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1e3b2240-8c87-44b4-a6b5-5f97f556b84a",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "subslide"
    },
    "tags": []
   },
   "source": [
    "## Fit $c$ coefficient\n",
    "\n",
    "- $c$ is then estimated for each symbol with simple linear regression\n",
    "$$\n",
    "\\hat{\\beta_i} = \\hat{\\alpha}_{M,i} + \\frac{\\hat{c}}{\\text{AD}_i^{\\hat{\\lambda}}} + \\hat{\\epsilon}_{M,i}\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "f7ab6e72-f204-4c78-8d3c-d4b8f7e72c84",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sym | c        \n",
      "----| ---------\n",
      "AMZN| 5.528774 \n",
      "BAC | 3.347822 \n",
      "CSCO| 1.83946  \n",
      "MU  | 0.2788601\n",
      "NFLX| 0.4830386\n",
      "ORCL| 0.2913703\n",
      "TSLA| 1.146609 \n"
     ]
    }
   ],
   "source": [
    "show c:select c:last last ols[1b;beta;1f%ad xexp lambda] by sym from  b lj l"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "adbafc12-4442-436d-b7d8-d767d792deab",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "subslide"
    },
    "tags": []
   },
   "source": [
    "## Compute $\\hat{\\beta}$\n",
    "\n",
    "- Using $c$, $\\lambda$ and the current market depth (AD), we can now\n",
    "  estimate the price impact of order flow imbalance (OFI)\n",
    "- What would the impact (in ticks) be of a single order at the bid\n",
    "  when we have typical market depth?\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "2ecb8ac3-4349-45e9-b47f-d2718d9856a3",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [],
   "source": [
    "beta:{[lambda;c;ad] c%ad xexp lambda}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "4a771e4a-2a8c-423c-b753-d8c19d67a7ca",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sym | lambda    c         ad       dticks      \n",
      "----| -----------------------------------------\n",
      "BAC | 1.399238  3.347822  498.5888 0.0005623118\n",
      "ORCL| 0.9505975 0.2913703 82.83719 0.00437507  \n",
      "CSCO| 1.471654  1.83946   57.11529 0.004779232 \n",
      "MU  | 1.045026  0.2788601 28.66863 0.008362934 \n",
      "NFLX| 0.2949058 0.4830386 2.745448 0.3586182   \n",
      "TSLA| 0.2956302 1.146609  3.395883 0.7988216   \n",
      "AMZN| 1.051458  5.528774  1.972146 2.707152    \n"
     ]
    }
   ],
   "source": [
    "t:l lj c lj select avg ad by sym from b\n",
    "`dticks xasc update dticks:beta[lambda;c;ad] from t"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4866735f-d81a-40e7-9b9b-71adff04df96",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": "subslide"
    },
    "tags": []
   },
   "source": [
    "# Less-than-average depth\n",
    "\n",
    "- What would the impact (in ticks) be of a single order at the bid\n",
    "  when we have less-than typical market depth?\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "8bee952e-bd35-4917-a22f-6b1859736641",
   "metadata": {
    "collapsed": false,
    "editable": true,
    "jupyter": {
     "outputs_hidden": false
    },
    "slideshow": {
     "slide_type": ""
    },
    "tags": []
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "sym | lambda    c         ad       dticks    \n",
      "----| ---------------------------------------\n",
      "BAC | 1.399238  3.347822  498.5888 0.01409989\n",
      "ORCL| 0.9505975 0.2913703 82.83719 0.03904654\n",
      "MU  | 1.045026  0.2788601 28.66863 0.09276506\n",
      "CSCO| 1.471654  1.83946   57.11529 0.1415834 \n",
      "NFLX| 0.2949058 0.4830386 2.745448 0.7071932 \n",
      "TSLA| 0.2956302 1.146609  3.395883 1.577902  \n",
      "AMZN| 1.051458  5.528774  1.972146 30.47691  \n"
     ]
    }
   ],
   "source": [
    "`dticks xasc update dticks:beta[lambda;c;.1*ad] from t"
   ]
  }
 ],
 "metadata": {
  "celltoolbar": "Slideshow",
  "kernelspec": {
   "argv": [
    "/Users/nick/miniconda3/bin/python",
    "-m",
    "ipykernel_launcher",
    "-f",
    "{connection_file}"
   ],
   "display_name": "Python 3 (ipykernel)",
   "env": null,
   "interrupt_mode": "signal",
   "language": "python",
   "metadata": {
    "debugger": false
   },
   "name": "python3"
  },
  "language_info": {
   "file_extension": ".q",
   "mimetype": "text/x-q",
   "name": "q",
   "version": "4.1.0"
  },
  "name": "09. Price Impact of Order Book Events.ipynb"
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
